model{

for(i in 1:M){
  for(j in f[i]:l[i]) {
    y[i,j] ~ dnorm(alpha0[i] + alpha1[i]*a[i,j] + eta[j], 1/sd_y[i]/sd_y[i])
  }
  alpha0[i] ~ dnorm(mu_a0, 1/sd_a0/sd_a0)
  alpha1[i] ~ dnorm(mu_a1, 1/sd_a1/sd_a1)T( ,0) # truncated norm
  
  sd_y[i] ~ dt(0, 0.04, 3)T(0, ) # variance differs independently by tree
}

mu_a0 ~ dnorm(0, 0.0001)
mu_a1 ~ dnorm(0, 0.01)T( , 0) # truncated normal on mean but not alpha_a1 with this formulation
sd_a0 ~ dt(0, 0.04, 3)T(0, )
sd_a1 ~ dt(0, 0.04, 3)T(0, )

alpha_t <- B %*% gamma

for(t in 1:Tea) {
  x[t] ~ dnorm(alpha_t[t], 1 / sd_x / sd_x)
  eta[t] ~ dnorm(beta0 * x[t], 1 / sd_eta / sd_eta)
}

for(h in 1:H){
  gamma[h] ~ dnorm(mu_gamma, 1 / sd_g / sd_g)
}

mu_gamma ~ dnorm(0, 0.00001)
sd_g ~ dt(0, 0.04, 3)T(0, )

# mu_x ~ dnorm(0, 0.0001)
sd_x ~ dt(0, 0.04, 3)T(0, )
beta0 ~ dnorm(0, 0.0001)T(0, ) # constrain to be positive to prevent 2 mode jumping with spline
sd_eta ~ dt(0, 0.04, 3)T(0, )

}

