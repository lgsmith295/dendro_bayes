model{

for(i in 1:M){
  for(j in f[i]:l[i]) {
    y[i,j] ~ dnorm(alpha0[i] + alpha1[i]*a[i,j] + eta[j, species[i]], 1/sd_y[i]/sd_y[i])
  }
 
 alpha0[i] ~ dnorm(0, 0.0001)
 alpha1[i] ~ dnorm(0, 0.0001)T( , 0) # truncated norm
  
  sd_y[i] ~ dt(0, 0.04, 3)T(0, ) # variance differs by core
}

# Non-centered
for(t in 1:Tea) {
  x[t] ~ dnorm(mu_x, 1 / sd_x / sd_x)
  for(k in 1:K) {
    eta[t, k] ~ dnorm(beta0[k, J[t, k]] * (x[t] - x_1[k]), 1 / sd_eta[k] / sd_eta[k]) # consider whether eta should be fixed at 0 below a certain x value. 
    J[t, k] <- 1 + step(x[t] - x_1[k])
  }
  
}

for(k in 1:K) {
x_1[k] ~ dunif(x_min, x_max)
}

mu_x ~ dnorm(0, 0.0001)
sd_x ~ dt(0, 0.04, 3)T(0, )
# sd_eta ~ dt(0, 0.04, 3)T(0, )
for(k in 1:K) {
  beta0[k, 1] ~ dnorm(0, 0.1)T(0, ) # strong prior to keep near zero
  beta0[k, 2] ~ dnorm(0, 0.0001)T(0, ) # response to climate above zero
  sd_eta[k] ~ dt(0, 0.04, 3)T(0, )
}

}